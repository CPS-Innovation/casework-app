resource "azurerm_linux_web_app_slot" "as_web_materials_staging1" {
  name                          = "staging1"
  app_service_id                = azurerm_linux_web_app.as_web_materials.id
  #virtual_network_subnet_id     = data.azurerm_subnet.materials_subnets[var.ui_subnet_name].id #TBC vnet integration is not required
  public_network_access_enabled = false
  https_only                    = true
  client_certificate_enabled    = false

  site_config {
    ftps_state              = "FtpsOnly"
    http2_enabled           = true
    app_command_line        = "npx serve -s"
    always_on               = true
    vnet_route_all_enabled  = true
    minimum_tls_version     = "1.2"
    scm_minimum_tls_version = "1.2"

    application_stack {
      node_version = "22-lts" #TBC
    }
  }

  app_settings = {
    "APPINSIGHTS_INSTRUMENTATIONKEY"  = azurerm_application_insights.ai_materials.instrumentation_key
    "HostType"                        = "Staging1"
  }

/*
  auth_settings_v2 {
    auth_enabled           = true
    require_authentication = true
    default_provider       = "AzureActiveDirectory"
    unauthenticated_action = "AllowAnonymous"
    excluded_paths         = ["/status"]

    # our default_provider:
    active_directory_v2 {
      tenant_auth_endpoint = "https://sts.windows.net/${data.azurerm_client_config.current.tenant_id}/v2.0"
      client_secret_setting_name = "MICROSOFT_PROVIDER_AUTHENTICATION_SECRET"
      client_id                  = module.azurerm_app_reg_as_web_polaris.client_id
    }

    # use a store for tokens (az blob storage backed)
    login {
      token_store_enabled = true
    }
  }
*/

  logs {
    detailed_error_messages = true
    failed_request_tracing  = true

    http_logs {
      file_system {
        retention_in_days = 7
        retention_in_mb   = 35
      }
    }
  }

  identity {
    type = "SystemAssigned"
  }

  tags = local.common_tags

}

resource "azurerm_private_endpoint" "pep_as_web_materials_staging1" {     
  name                = "${azurerm_linux_web_app.as_web_materials.name}-staging1-pe"
  location            = azurerm_resource_group.rg_materials.location
  resource_group_name = azurerm_resource_group.rg_materials.name
  subnet_id           = data.azurerm_subnet.materials_subnets[var.app_subnet_name].id

  private_service_connection {
    name                           = "${azurerm_linux_web_app.as_web_materials.name}-staging1"
    private_connection_resource_id = azurerm_linux_web_app.as_web_materials.id
    subresource_names              = ["sites-staging1"]
    is_manual_connection           = false
  }

  private_dns_zone_group {
    name                 = "app-dns-zone-group"
    private_dns_zone_ids = [data.azurerm_private_dns_zone.hub_dns_zones["sites"].id]
  }

  tags  = local.common_tags


  dynamic "ip_configuration" {
    for_each = var.as_web_pe_ip_staging == null ? [] : [1]
    content {
      name               = "ip-${azurerm_linux_web_app.as_web_materials.name}"
      private_ip_address = var.as_web_pe_ip_staging
      subresource_name   = "sites-staging1"
      member_name        = "sites-staging1"
    }
  }


  depends_on = [
    azurerm_linux_web_app_slot.as_web_materials_staging1
  ]
}